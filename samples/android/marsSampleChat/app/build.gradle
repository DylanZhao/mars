apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    signingConfigs {

        samplerelease {
            keyAlias 'tencentmars'
            keyPassword 'android'
            storeFile rootProject.file('app/release.jks')
            storePassword 'android'
        }
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.1'
    defaultConfig {
        applicationId 'com.tencent.mars.sample'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName '1.0.0'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            proto {
                srcDir "src/main/proto"
            }
            java {
                srcDir 'src/main/java'
            }

            res {
                srcDir 'src/main/res'
            }
        }
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        MarsSample {
            proguardFile rootProject.file('app/proguard-rules.pro')
            signingConfig signingConfigs.samplerelease
            targetSdkVersion 25
            minSdkVersion 16
            versionCode 1
            versionName '1.0.0'
        }
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
}

ext {
    useLocalMarsWrapper = true // Changed to true is better for sample development.
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:25.1.1'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-4'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    if (useLocalMarsWrapper) {
        compile project(':wrapper')

    } else {
        compile "com.tencent.mars:mars-wrapper:${VERSION_NAME}${VERSION_NAME_SUFFIX}"
    }
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
